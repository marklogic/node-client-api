// Only used for setting up the test AppServer in MarkLogic
plugins {
  id 'net.saliman.properties' version '1.5.2'
  // Sticking with 5.0.0 until a bug in 6.0.x is fixed where LSQT config isn't deployed correctly.
  id "com.marklogic.ml-gradle" version "5.0.0"
}

tasks.register("addMarkLogic12SchemasIfNecessary", com.marklogic.gradle.task.MarkLogicTask) {
  description = "If testing against MarkLogic 12, include schemas that will not work on MarkLogic 11 or earlier."
  doLast {
    def version = new com.marklogic.mgmt.resource.clusters.ClusterManager(getManageClient()).getVersion()
    if (version.startsWith("12.")) {
      mlAppConfig.getSchemaPaths().add(new File(getProjectDir(), "src/main/ml-schemas-12").getAbsolutePath())
    }
  }
}
mlDeploy.dependsOn addMarkLogic12SchemasIfNecessary
mlLoadSchemas.dependsOn addMarkLogic12SchemasIfNecessary

/*
 * Required for forcing MarkLogic to generate a temporary certificate for the app server
 * that requires SSL.
 */
ext {
    def command = new com.marklogic.appdeployer.command.security.GenerateTemporaryCertificateCommand()
    command.setTemplateIdOrName("node-client-ssl-template")
    command.setCommonName("localhost")
    command.setValidFor(365)
    mlAppDeployer.commands.add(command)
}
